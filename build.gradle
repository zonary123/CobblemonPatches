plugins {
  id 'fabric-loom' version '1.12-SNAPSHOT'
  id 'maven-publish'
}

ext.hash_id = ""

// Leer isDev desde gradle.properties (default: false)
def isDev = project.hasProperty("isDev") ? project.property("isDev").toBoolean() : false

// Si es dev, generar hash de git, sino no hace falta
if (isDev) {
  try {
    def stdout = new ByteArrayOutputStream()
    exec {
      commandLine 'git', 'rev-parse', '--short', 'HEAD'
      standardOutput = stdout
    }
    ext.hash_id = stdout.toString().trim()
  } catch (Exception e) {
    ext.hash_id = "dev"
  }
}

// Configurar el nombre del archivo
archivesBaseName = project.archives_base_name

// Version del mod (con sufijo dev si corresponde)
version = project.mod_version + (isDev ? "-dev-${hash_id}" : "")

group = project.maven_group

loom {
  splitEnvironmentSourceSets()

  mods {
    "cobblemonpatches" {
      sourceSet sourceSets.main
      sourceSet sourceSets.client
    }
  }
}


repositories {
  mavenCentral()
  gradlePluginPortal()
  maven { url "https://jitpack.io" }
  maven { url "https://cursemaven.com" }
  maven { url "https://maven.fabricmc.net/" }
  maven { url "https://maven.architectury.dev/" }
  maven { url "https://repo.maven.apache.org/maven2/" }
  maven { url "https://repo.spongepowered.org/maven/" }
  maven { url "https://repo.essentialsx.net/releases/" }
  maven { url "https://maven.neoforged.net/releases/" }
  maven { url "https://files.minecraftforge.net/maven/" }
  maven { url "https://thedarkcolour.github.io/KotlinForForge/" }
  maven { url "https://papermc.io/repository/maven-public/" }
  maven { url "https://maven.impactdev.net/repository/development/" }
  maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
  maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
  maven { url "https://maven.nucleoid.xyz/"; name = "Nucleoid" }
  maven { url "https://oss.sonatype.org/content/repositories/snapshots"; name = "Sonatype Snapshots" }
  maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots"; name = "Sonatype 01 Snapshots" }
}

dependencies {
  // To change the versions see the gradle.properties file
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
  modImplementation(include('com.github.ben-manes.caffeine:caffeine:3.1.8'))

  modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
  modImplementation("com.cobblemon:fabric:${property("cobblemon_version")}")
  modImplementation("net.fabricmc:fabric-language-kotlin:1.12.3+kotlin.2.0.21")
  implementation("com.github.ben-manes.caffeine:caffeine:3.2.3")
}

processResources {
  var properties = [
    "version": version,
    "minecraft_version": project.minecraft_version,
    "loader_version": project.loader_version,
    "cobblemon_version": project.cobblemon_version
  ]
  for (property in properties) {
    inputs.property property.key, property.value
  }
  filteringCharset "UTF-8"

  filesMatching("fabric.mod.json") {
    expand properties
  }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
  it.options.encoding = "UTF-8"
  if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
    it.options.release.set(targetJavaVersion)
  }
}

java {
  def javaVersion = JavaVersion.toVersion(targetJavaVersion)
  if (JavaVersion.current() < javaVersion) {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
  }
  withSourcesJar()
}

jar {
  from("LICENSE") {
    rename { "${it}_${project.archivesBaseName}" }
  }
}

publishing {
  publications {
    create("mavenJava", MavenPublication) {
      artifactId = project.archives_base_name
      from components.java
    }
  }
}
