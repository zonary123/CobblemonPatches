plugins {
  id 'fabric-loom' version "${loom_version}"
  id 'maven-publish'
  id "org.jetbrains.kotlin.jvm" version "2.2.21"
}

ext.hash_id = ""

// Leer isDev desde gradle.properties (default: false)
def isDev = project.hasProperty("isDev") ? project.property("isDev").toBoolean() : false

// Si es dev, generar hash de git, sino no hace falta
if (isDev) {
  try {
    ext.hash_id = 'git rev-parse --short HEAD'.execute().text.trim()
  } catch (Exception ignored) {
    ext.hash_id = "unknown"
  }
}

// Version del mod (con sufijo dev si corresponde)
version = project.mod_version + (isDev ? "-dev-${hash_id}" : "")

group = project.maven_group

// Configurar el nombre del archivo
base {
  archivesName = project.archives_base_name
}

repositories {
  maven { url = "https://maven.impactdev.net/repository/development/" }
}

dependencies {
  // To change the versions see the gradle.properties file
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

  modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
  modImplementation("net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}")

  modImplementation("com.cobblemon:fabric:${project.cobblemon_version}")
  implementation(include("com.github.ben-manes.caffeine:caffeine:${project.caffeine_version}"))
}

processResources {
  var properties = [
    "version"          : version,
    "minecraft_version": project.minecraft_version,
    "loader_version"   : project.loader_version,
    "cobblemon_version": project.cobblemon_version
  ]
  for (property in properties) {
    inputs.property property.key, property.value
  }
  filteringCharset = "UTF-8"

  filesMatching("fabric.mod.json") {
    expand properties
  }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
  it.options.encoding = "UTF-8"
  if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
    it.options.release.set(targetJavaVersion)
  }
}

java {
  def javaVersion = JavaVersion.toVersion(targetJavaVersion)
  if (JavaVersion.current() < javaVersion) {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
  }
  withSourcesJar()
}

jar {
  from("LICENSE") {
    rename { "${it}_${project.archivesBaseName}" }
  }
}

publishing {
  publications {
    create("mavenJava", MavenPublication) {
      artifactId = project.archives_base_name
      from components.java
    }
  }
}
